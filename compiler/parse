#!/usr/bin/perl

use strict;
use warnings;

use List::Util qw(any);

my $s = join('',<>);
my @s = split //, $s;
my $pos = 0;
my $line = 1;
my $col = 1;
my $labelnum = 0;

my $scope = {
    global => 1,
    symbols => {},
    parent => undef,
};

my @stack;

parseProgram();
if ($pos != @s) {
    print "Ended at $pos (line $line, col $col): had " . (@s-$pos) . " chars left over\n";
}

# recursive descent parser based on https://www.youtube.com/watch?v=Ytq0GQdnChg
# each parseFoo() parses a rule from the grammar:
#  - if it matches, return true and $pos points at the next character in the input
#  - if it doesn't match, return false and leave $pos unchanged
#
# TODO: get out of manually restoring $pos, make something do it for us
# TODO: code generation
# TODO: restore line/col when pos is restored
# TODO: some tracing of call stack for debugging
# TODO: only skip() after terminals
# TODO: order the disjunctions so that the most-specific ones come first (i.e. don't let keywords
#       match as identifiers in expressions if we can unambiguously identify them as their actual
#       keyword)
# TODO: annotate generated assembly code with the source code that generated it

sub parseProgram {
    return 0 if !parseStatements();

    die "expected to be left in global scope after program" if !$scope->{global};

    # allocate space for globals
    for my $global (keys %{ $scope->{symbols} }) {
        print "$global: .word 0 # global\n";
    }

    return 1;
}

sub parseStatements {
    while (1) {
        skip();
        return 1 if !parseStatement();
        skip();
        return 1 if !parseChar(';');
    }
}

sub parseStatement {
    return 1 if parseBlock() || parseExtern() || parseDeclaration() || parseConditional() || parseLoop() || parseReturn() || parseAssignment() || parseExpression();
    return 0;
}

# create new nested scope
sub parseBlock {
    return 0 if !parseChar('{');
    skip();

    newscope();

    parseStatements() && skip();

    die "block must end with }" if !parseChar('}');
    skip();

    endscope();

    return 1;
}

sub newscope {
    # save old base pointer, and put new base pointer in r253
    print "# newscope:\n";
    print "ld x, r253\n";
    print "push x\n";
    print "ld r253, r255\n";

    $scope = {
        symbols => {},
        parent => $scope,
        bp_rel => 1,
    };
}

sub endscope {
    # restore old base pointer
    print "# endscope: \n";
    print "pop x\n";
    print "ld r253, x\n";

    die "can't exit global scope" if $scope->{global};

    $scope = $scope->{parent};
}

# add name to current scope (possibly global)
sub parseDeclaration {
    return 0 if !parseString('var');
    skip();
    die "var declaration needs identifier" if !parseIdentifier();
    skip();
    my $id = pop @stack;
    die "var declaration expected identifier" if $id->{type} ne 'identifier';

    die "duplicate declaration in scope: $id->{name}" if $scope->{symbols}{$id->{name}};

    # add name to scope
    $scope->{symbols}{$id->{name}} = {
        type => $scope->{global} ? 'global' : 'local',
        bp_rel => $scope->{bp_rel}++,
    };

    return 1 if !parseChar('=');
    skip();

    # now an expression to initialise the variable
    parseExpression();
    skip();

    storetovar($id->{name});
    return 1;
}

sub storetovar {
    my ($name) = @_;

    print "# storetovar $name:\n";

    if ($scope->{global}) {
        # load into the global variable
        print "pop x\n";
        print "ld ($name), x\n";
    } else {
        my $bp_rel = $scope->{symbols}{$name}{bp_rel};
        # pop into r0, then store into bp_rel relative to base pointer
        print "pop x\n";
        print "ld r0, x\n";

        print "ld x, r253\n";
        print "add x, $bp_rel\n";
        print "ld (x), r0\n";
    }

    return 1;
}

sub symlookup {
    my ($name) = @_;

    my $s = $scope;

    while ($s) {
        return $s->{symbols}{$name} if $s->{symbols}{$name};
        $s = $s->{parent};
    }
}

# add name to global scope, but don't allocate any space for it
sub parseExtern {
    my $pos0 = $pos;
    return 1 if parseString('extern') && skip()
        && parseIdentifier() && skip();
    $pos = $pos0;
    return 0;
}

# stack will have rvalue at top and lvalue below.
# pop lvalue into r0
# pop rvalue into r1
# generate: ld (r1), r0
sub parseAssignment {
    my $pos0 = $pos;
    return 1 if
        parseLValue() && skip()
        && parseChar('=') && skip()
        && parseExpression() && skip();
    $pos = $pos0;
    # TODO: use storetovar() if the l-value is an identifier
    return 0;
}

# push address of lvalue onto stack
# globals: easy
# locals: ???
# maybe we say all declarations in a scope have to come before all statements
sub parseLValue {
    my $pos0 = $pos;
    return 1 if parseIdentifier();
    return 0 if !parseChar('*');
    return 1 if parseExpression();
    $pos = $pos0;
    return;
}

# after parseExpression, branch to elselabel if false, then let parseStatement
# generate its code followed by a jump to the endiflabel, then put the elselabel,
# and if there's an "else" then let another parseStatement generate its code, then put the endiflabel
sub parseConditional {
    my $pos0 = $pos;
    return 1 if parseString("if") && skip()
        && parseChar('(') && skip()
        && parseExpression() && skip()
        && parseChar(')') && skip()
        && parseStatement() && skip()
        && parseString("else") && skip()
        && parseStatement() && skip();
    $pos = $pos0;
    return 1 if parseString("if") && skip()
        && parseChar('(') && skip()
        && parseExpression() && skip()
        && parseChar(')') && skip()
        && parseStatement() && skip();
    $pos = $pos0;
    return 0;
}

# write a looplabel, then after parseExpression branch to endwhilelabel if false, then let
# parseStatement write its code, then jump to looplabel
sub parseLoop {
    my $pos0 = $pos;
    return 1 if parseString('while') && skip()
        && parseChar('(') && skip()
        && parseExpression() && skip()
        && parseChar(')') && skip()
        && parseStatement() && skip();
    $pos = $pos0;
    return 0;
}

# pop args off stack and 'ret'
sub parseReturn {
    my $pos0 = $pos;
    return 1 if parseString('return') && skip()
        && parseExpression() && skip();
    $pos = $pos0;
    return 0;
}

sub parseConstant {
    return parseNumericLiteral() || parseStringLiteral() || parseFunctionDeclaration();
}

sub parseNumericLiteral {
    return parseHexLiteral() || parseBinaryLiteral() || parseCharacterLiteral() || parseDecimalLiteral();
}

sub parseHexLiteral {
    return 0 if !parseString("0x");
    my $pos0 = $pos;
    if (!matchChar(qr/[0-9a-fA-F]/)) {
        die "illegal hex literal\n";
    }
    while (matchChar(qr/[0-9a-fA-F]/)) { }

    my $val = substr($s, $pos0, $pos-$pos0);
    genliteral(hex($val));

    skip();
    return 1;
}

sub parseBinaryLiteral {
    return 0 if !parseString("0b");
    my $pos0 = $pos;
    if (!matchChar(qr/[01]/)) {
        die "illegal binary literal\n";
    }
    while (matchChar(qr/[01]/)) { }

    my $val = substr($s, $pos0, $pos-$pos0);
    genliteral(oct("0b$val"));

    skip();
    return 1;
}

sub parseCharacterLiteral {
    my $pos0 = $pos;
    return 0 if !parseChar("'");
    my $c = $s[$pos++];
    if ($c eq '\\') {
        # TODO: special-case these
        genliteral(ord($s[$pos]));
        $pos++;
    } else {
        genliteral(ord($c));
    }
    
    return 1 if parseChar("'");
    die "illegal character literal\n";
    $pos = $pos0;
    return 0;
}

sub parseDecimalLiteral {
    my $pos0 = $pos;
    return 0 if !matchChar(qr/[-+0-9]/);
    while (matchChar(qr/\d/)) {}

    my $val = substr($s, $pos0, $pos-$pos0);
    genliteral($val);

    skip();
    return 1;
}

sub genliteral {
    my ($val) = @_;

    print "# genliteral:\n";

    $val += 0x10000 if $val < 0; # make everything positive

    die "numeric literal out of range: $val\n" if $val < 0 || $val > 0xffff;

    if ($val <= 0x00ff || $val >= 0xff00) {
        print "push $val\n";
    } else {
        print "ld x, $val\n";
        print "push x\n";
    }
}

# write a jump to endofstringlabel, write the string, write the endofstringlabel
sub parseStringLiteral {
    return 0 if !parseChar('"');
    my $startstring = label();
    my $endstring = label();

    # XXX: generate string in-line
    # TODO: add it to a table of string constants and write them at the end of the program

    print "# parseStringLiteral:\n";

    print "jmp $endstring\n";
    print "$startstring:\n";
    while ($pos < @s) {
        if (parseChar('"')) {
            skip();

            print ".word 0\n"; # terminate
            print "$endstring:\n";
            print "ld x, $startstring\n";
            print "push x\n";
            return 1;
        }
        if (parseChar('\\')) {
            # TODO: handle escaped chars
        } else {
            print ".word " . ord($s[$pos]) . "\n";
        }
        $pos++;
    }
    die "un-terminated string literal (started at line ... col ...)\n";
}

sub parseFunctionDeclaration {
    return 0 if !parseString('func');
    skip();

    print "\n# parseFunctionDeclaration:\n";

    die "expected (" if !parseChar('(');
    skip();

    my $sp0 = @stack;
    parseParameters();
    skip();

    newscope();

    while (@stack != $sp0) {
        my $id = pop @stack;
        die "expected identified" if $id->{type} ne 'identifier';
        die "duplicate parameter name: $id->{name}" if $scope->{symbols}{$id->{name}};
        $scope->{symbols}{$id->{name}} = {
            type => 'local',
            bp_rel => $scope->{bp_rel}++,
        };
    }

    die "expected )" if !parseChar(')');
    skip();

    parseStatement();
    skip();
    endscope();

    # implicit "return 0"
    print "push 0\n";
    print "ret\n";
    print "# end function declaration\n\n";

    return 1;
}

# somehow return a list of the parameter names to parseFunctionDeclaration()
sub parseParameters {
    while (1) {
        skip();
        return 1 if !parseIdentifier();
        skip();
        return 1 if !parseChar(',');
    }
}

sub parseFunctionCall {
    my $pos0 = $pos;
    my $sp0 = @stack;
    if (parseIdentifier() && skip() # TODO: allow "expression" instead of "identifier"
        && parseChar('(') && skip()) {
        print "# parseFunctionCall:\n";
        # backup r254 before we push arguments
        print "ld x, r254\n";
        print "push x\n";

        if (parseArguments() && skip()
            && parseChar(')') && skip()) {
            my $identifier = $stack[$sp0];
            die "identifier type != identifier" if $identifier->{type} ne 'identifier';

            # call function
            print "call $identifier->{name}\n";

            # restore r254
            print "pop x\n";
            print "ld r254, x\n";

            pop @stack while @stack > $sp0;
            return 1;
        } else {
            die "illegal function call\n";
        }
    }
    $pos = $pos0;
    return 0;
}

sub parseArguments {
    while (1) {
        skip();
        return 1 if !parseExpression();
        skip();
        return 1 if !parseChar(',');
    }
}

sub parseUnaryExpression {
    my $op = $s[$pos];
    return 0 if !matchChar(qr/[!~]/);
    skip();
    if (!parseExpression()) {
        die "unary operator $op must be followed by an expression";
    }

    if ($op eq '~') {
        # pop arg from stack, negate it, put it back on stack
        print "# unary bitwise complement:\n";
        print "pop x\n";
        print "not x\n";
        print "push x\n";
    } elsif ($op eq '!') {
        # test arg from stack, if it's zero do nothing, otherwise overwrite it with a 1
        print "unary not:\n";
        my $endlabel = label();
        print "ld x, 1(sp)\n";
        print "test x\n";
        print "jz $endlabel\n";
        print "ld 1(sp), 1\n";
        print "$endlabel:\n";
    }

    return 1;
}

sub label {
    $labelnum++;
    return "l__$labelnum";
}

# while loop: pop 2 args off stack and apply expression
sub parseExpression {
    my $pos0 = $pos;
    while (1) {
        if (!parseBinary1()) {
            $pos = $pos0;
            return 0;
        }
        skip();
        return 1 if !matchChar(qr/[\*\/%]/);
        skip();
    }
}

# while loop: pop 2 args off stack and apply expression
sub parseBinary1 {
    my $pos0 = $pos;
    while (1) {
        if (!parseBinary2()) {
            $pos = $pos0;
            return 0;
        }
        skip();
        return 1 if !matchChar(qr/[\+\-]/);
        skip();
    }
}

# while loop: pop 2 args off stack and apply expression
sub parseBinary2 {
    my $pos0 = $pos;
    while (1) {
        if (!parseBinary3()) {
            $pos = $pos0;
            return 0;
        }
        skip();
        return 1 if !matchChar(qr/[&|^]/);
        skip();
    }
}

# while loop: pop 2 args off stack and apply expression
sub parseBinary3 {
    my $pos0 = $pos;
    while (1) {
        if (!parseBinary4()) {
            $pos = $pos0;
            return 0;
        }
        skip();
        return 1 if !(parseString("==") || parseString("!=") || parseString(">=") || parseString("<=") || matchChar(qr/[<>]/));
        skip();
    }
}

# while loop: pop 2 args off stack and apply expression
sub parseBinary4 {
    my $pos0 = $pos;
    while (1) {
        if (!parseBinary5()) {
            $pos = $pos0;
            return 0;
        }
        skip();
        return 1 if !(parseString("&&") || parseString("||"));
        skip();
    }
}

sub parseBinary5 {
    return 1 if parseConstant() || parseFunctionCall() || parseUnaryExpression() || parseParenExpression();

    return 0 if !parseIdentifier();
    my $id = pop @stack;
    die "expected identifier" if $id->{type} ne 'identifier';

    # look up the identifier and push it
    my $sym = symlookup($id->{name});
    die "unrecognised identifier: $id->{name}\n" if !$sym;
    if ($sym->{global}) {
        print "# global $id->{name}\n";
        print "ld x, ($id->{name})\n";
        print "push x\n";
    } else {
        print "# local $id->{name}\n";
        print "ld x, r253\n";
        print "add x, $sym->{bp_rel}\n";
        print "ld x, (x)\n";
        print "push x\n";
    }

    return 1;
}

sub parseParenExpression {
    my $pos0 = $pos;
    return 0 if !parseChar('(');
    skip();
    return 1 if parseExpression() && skip() && parseChar(')') && skip();
    $pos = $pos0;
    return 0;
}

sub parseIdentifier {
    my $pos0 = $pos;
    return 0 if !matchChar(qr/[a-zA-Z_]/);
    while (matchChar(qr/[a-zA-Z0-9_]/)) {};
    push @stack, {
        type => 'identifier',
        name => substr($s, $pos0, $pos-$pos0),
    };
    skip();
    return 1;
}

sub parseString {
    my ($s) = @_;
    my $pos0 = $pos;
    for my $c (split //, $s) {
        if (!parseChar($c)) {
            $pos = $pos0;
            return 0;
        }
    }
    return 1;
}

sub parseChar {
    my ($c) = @_;
    return matchChar(qr/\Q$c\E/);
}

sub matchChar {
    my ($re) = @_;
    return 0 if $pos >= @s;
    return 0 if $s[$pos] !~ /$re/;
    if ($s[$pos] eq "\n") {
        $line++;
        $col = 0;
    }
    $pos++;
    $col++;
    return 1;
}

# skip over whitespace and comments
sub skip {
    while (1) {
        if (parseChar('#')) {
            while (matchChar(qr/[^\n]/)) { } # skip everything until \n
        } elsif (!matchChar(qr/[ \t\r\n]/)) {
            return 1;
        }
    }
}
